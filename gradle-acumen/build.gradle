apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'idea'

group = 'com.github.rholder'
version = '0.3.0'

configurations {
    bundled
}

dependencies {
    compile gradleApi()
    compile project(':gradle-acumen-api')

    // we want to include gradle-acumen-api classes inside the final jar
    bundled(project(':gradle-acumen-api')) {
        transitive = false
    }

    // include groovy-backports-compat23 for org.codehaus.groovy.runtime.typehandling.ShortTypeHandling
    // https://jira.codehaus.org/browse/GROOVY-6973
    // Groovy 2.3.9 is used in Gradle 2.3 so let's use that groovy-backports-compat23
    bundled 'org.codehaus.groovy:groovy-backports-compat23:2.3.9'
}

jar {
    // include anything in the bundled configuration in the final jar
    from {
        configurations.bundled.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task sourceJar(type: Jar) {
      description "An archive of the source code for Maven Central"
      classifier "sources"
      from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

